/* Public.Parser.Yaml
 *
 * Copyright (C) 2013  Pontus Ã–stlund
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along
 * with this program; if not, write to the Free Software Foundation, Inc.,
 * 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
 */

#pike __REAL_VERSION__

//! @ignore
inherit Public.Parser@module@;

constant __components = ({});
//! @endignore

class Parser
{
  inherit _Parser;

  private constant STATE_KEY = 0;

  //! Parse @[file_or_data] into a mapping
  //!
  //! @param file_or_data
  //!  Either a path to a file or the YAML tree as a string
  mapping get_mapping (string file_or_data)
  {
    if (Stdio.exist (file_or_data))
      set_input_file (file_or_data);

    mapping out = ([]);

    int type;
    int(0..1) data_state;
    mapping tmp_map;
    array tmp_arr;
    string last_key, seq_last_key;

    while ((type = parse ()) != STREAM_END_EVENT) {
      switch (type)
      {
        case SEQUENCE_START_EVENT:
          if (tmp_map) {
            out += tmp_map;
            tmp_map = 0;
          }

          data_state = STATE_KEY;
          seq_last_key = last_key;
          tmp_arr = ({});
          break;

        case MAPPING_START_EVENT:
          tmp_map = ([]);
          break;

        case SEQUENCE_END_EVENT:
          if (tmp_map)
            tmp_map[seq_last_key] = tmp_arr;
          else
            out[seq_last_key] = tmp_arr;

          tmp_arr = 0;
          break;

        case MAPPING_END_EVENT:
          if (tmp_map) {
            if (tmp_arr)
              tmp_arr += ({ tmp_map });
            else
              out += tmp_map;

            tmp_map = 0;
          }
          break;

        case SCALAR_EVENT:
          mapping value = get_event ();

          if (data_state == STATE_KEY)
            last_key = value->scalar->value;
          else
            tmp_map[last_key] = value->scalar->value;

          data_state = !data_state;

          break;
      }
    }

    return out;
  }
}
